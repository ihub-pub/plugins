/*
 * Copyright (c) 2021 Henry 李恒 (henry.box@outlook.com).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'pub.ihub.plugin'
    id 'pub.ihub.plugin.ihub-git-hooks'
    id 'com.gradle.plugin-publish' apply false
    // Jacoco暂时支持TestKit，如下插件用于集成Jacoco报告，详见：https://github.com/gradle/gradle/issues/1465
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.9'
}

iHubGitHooks {
    hooks = [
        'pre-commit': './gradlew build -x test'
    ]
}

subprojects {
    apply {
        plugin 'groovy'
        plugin 'pub.ihub.plugin.ihub-java'
        plugin 'pub.ihub.plugin.ihub-test'
        plugin 'pub.ihub.plugin.ihub-verification'
        plugin 'pub.ihub.plugin.ihub-publish'
        plugin 'java-gradle-plugin'
        plugin 'com.gradle.plugin-publish'
        plugin 'pl.droidsonroids.jacoco.testkit'
    }

    dependencies {
        implementation gradleApi()
    }

    pluginBundle {
        website     = 'https://github.com/ihub-pub/plugins'
        vcsUrl      = 'https://github.com/ihub-pub/plugins.git'
        description = 'IHub Plugins Gradle Plugin'
        tags        = ['ihub', 'groovy', 'gradle', 'spring', 'boot', 'java', 'native']
    }

    // 跳过Gradle元数据生成，详见：https://github.com/gradle/gradle/issues/11862
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

/*
 * Copyright (c) 2021 Henry 李恒 (henry.box@outlook.com).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'pub.ihub.plugin'
    id 'com.gradle.plugin-publish' apply false
    // Jacoco暂时支持TestKit，如下插件用于集成Jacoco报告，详见：https://github.com/gradle/gradle/issues/1465
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.9'
}

apply {
    plugin 'java-platform'
    plugin 'pub.ihub.plugin.ihub-publish'
}

iHubGitHooks {
    hooks = [
        'pre-commit': './gradlew build -x test',
        'commit-msg': './gradlew commitCheck'
    ]
}

//<editor-fold desc="组件依赖版本管理">

repositories {
    gradlePluginPortal()
}

javaPlatform {
    allowDependencies()
}

iHubBom.bomVersions.clear()

dependencies {
    constraints {
        api 'pub.ihub.lib:ihub-core:1.0.17',
            'io.spring.gradle:dependency-management-plugin:1.1.0',
            'io.freefair.gradle:plugin-utils:6.6.1',
            'io.freefair.gradle:git-plugin:6.6.1',
            'io.freefair.gradle:lombok-plugin:6.6.1',
            'io.freefair.gradle:github-plugin:6.6.1',
            'com.github.ben-manes:gradle-versions-plugin:0.44.0',
            'gradle.plugin.net.bytebuddy:byte-buddy-gradle-plugin:1.12.12',
            'org.jmolecules.integrations:jmolecules-bytebuddy:1.6.0',
            'com.github.johnrengelman.processes:com.github.johnrengelman.processes.gradle.plugin:0.5.0',
            'org.springdoc:springdoc-openapi-gradle-plugin:1.6.0',
            'org.springframework.boot:spring-boot-gradle-plugin:3.0.1',
            'org.graalvm.buildtools:native-gradle-plugin:0.9.19'
    }
}

//</editor-fold>

subprojects {
    apply {
        plugin 'pub.ihub.plugin.ihub-groovy'
        plugin 'pub.ihub.plugin.ihub-test'
        plugin 'pub.ihub.plugin.ihub-verification'
        plugin 'pub.ihub.plugin.ihub-publish'
        plugin 'java-gradle-plugin'
        plugin 'com.gradle.plugin-publish'
        plugin 'pl.droidsonroids.jacoco.testkit'
    }

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        implementation platform(rootProject)
        implementation gradleApi()
        if (!(project in [project(':ihub-aware'), project(':ihub-test')])) {
            implementation project(':ihub-aware')
            testImplementation project(':ihub-test')
        }
    }

    pluginBundle {
        website     = 'https://github.com/ihub-pub/plugins'
        vcsUrl      = 'https://github.com/ihub-pub/plugins.git'
        description = 'IHub Plugins Gradle Plugin'
        tags        = ['ihub', 'groovy', 'spring', 'boot', 'java', 'native']
    }

    // 跳过Gradle元数据生成，详见：https://github.com/gradle/gradle/issues/11862
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}
